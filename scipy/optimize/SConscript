# Last Change: Sat May 03 02:00 PM 2008 J
# vim:syntax=python

import os
from os.path import join as pjoin, splitext

from numpy.distutils.misc_util import get_numpy_include_dirs
from numscons import get_python_inc#, get_pythonlib_dir
from numscons import GetNumpyEnvironment
from numscons import CheckF77LAPACK, CheckF77Clib

from numscons import write_info

env = GetNumpyEnvironment(ARGUMENTS)
env.Tool('f2py')
env.Append(CPPPATH = get_numpy_include_dirs())
env.Append(CPPPATH = env['F2PYINCLUDEDIR'])
#if os.name == 'nt':
#    # NT needs the pythonlib to run any code importing Python.h, including
#    # simple code using only typedef and so on, so we need it for configuration
#    # checks
#    env.AppendUnique(LIBPATH = [get_pythonlib_dir()])

#=======================
# Starting Configuration
#=======================
config = env.NumpyConfigure(custom_tests = {'CheckLAPACK' : CheckF77LAPACK,
                                            'CheckF77Clib' : CheckF77Clib})

#-----------------
# Checking Lapack
#-----------------
if not config.CheckF77Clib():
    raise RuntimeLibrary("Could not check C/F runtime library for %s/%s"\
                         " , contact the maintainer" % (env['CC'], env['F77']))

st = config.CheckLAPACK()
if not st:
    has_lapack = 0
else:
    has_lapack = 1

config.Finish()
write_info(env)

#==========
#  Build
#==========

# minpack lib
minpack_src = [str(s) for s in env.NumpyGlob(pjoin('minpack', '*.f'))]
env.NumpyStaticExtLibrary('minpack', source = minpack_src)

# rootfind lib
rootfind_src = [str(s) for s in env.NumpyGlob(pjoin('Zeros', '*.c'))]
env.NumpyStaticExtLibrary('rootfind', source = rootfind_src)

env.AppendUnique(LIBS = ['minpack', 'rootfind'])
env.AppendUnique(LIBPATH = env['build_dir'])

# _minpack pyextension
env.DistutilsPythonExtension('_minpack', '_minpackmodule.c',
                         LINKFLAGSEND = env['F77_LDFLAGS'])

# _zeros pyextension
env.DistutilsPythonExtension('_zeros', 'zeros.c')

# _lbfgsb pyextension
src = [pjoin('lbfgsb', i) for i in ['lbfgsb.pyf', 'routines.f']]
env.DistutilsPythonExtension('_lbfgsb', source = src,
                         LINKFLAGSEND = env['F77_LDFLAGS'])

# _cobyla pyextension
src = [pjoin('cobyla', i) for i in ['cobyla2.f', 'trstlp.f', 'cobyla.pyf']]
env.DistutilsPythonExtension('_cobyla', source = src,
                         LINKFLAGSEND = env['F77_LDFLAGS'])

# _minpack2 pyextension
src = [pjoin('minpack2', i) for i in ['dcsrch.f', 'dcstep.f', 'minpack2.pyf']]
env.DistutilsPythonExtension('minpack2', source = src,
                         LINKFLAGSEND = env['F77_LDFLAGS'])

# moduleTNC pyextension
env.DistutilsPythonExtension('moduleTNC', 
                         source = [pjoin('tnc', i) for i in \
                                                   ['moduleTNC.c', 'tnc.c']])

# _slsqp pyextension
src = [pjoin('slsqp', i) for i in ['slsqp_optmz.f', 'slsqp.pyf']]
env.DistutilsPythonExtension('_slsqp', source = src,
                         LINKFLAGSEND = env['F77_LDFLAGS'])
