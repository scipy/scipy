# Last Change: Sat Nov 24 05:00 PM 2007 J
# vim:syntax=python

import os
from os.path import join as pjoin, splitext

from numpy.distutils.misc_util import get_numpy_include_dirs
from numpy.distutils.scons import get_python_inc
from numpy.distutils.scons import GetNumpyEnvironment
from numpy.distutils.scons import CheckF77LAPACK,\
                                  CheckCLAPACK, \
                                  IsATLAS, GetATLASVersion

from scons_support import do_generate_fake_interface, \
                          generate_interface_emitter
from numpy.distutils.scons.configuration import write_info

env = GetNumpyEnvironment(ARGUMENTS)
env.Append(CPPPATH = [get_python_inc(), get_numpy_include_dirs()])
#if os.name == 'nt':
#    # NT needs the pythonlib to run any code importing Python.h, including
#    # simple code using only typedef and so on, so we need it for configuration
#    # checks
#    env.AppendUnique(LIBPATH = [get_pythonlib_dir()])

#=======================
# Starting Configuration
#=======================
config = env.NumpyConfigure(custom_tests = {'CheckCBLAS' : CheckCBLAS,
                                            'CheckBLAS' : CheckF77BLAS,
                                            'CheckCLAPACK' : CheckCLAPACK,
                                            'CheckLAPACK' : CheckF77LAPACK})

#--------------
# Checking Blas
#--------------
st = config.CheckLAPACK(check_version = 1)
if not st:
    raise RuntimeError("no lapack found, necessary for lapack module")

if IsATLAS(env, 'lapack'):
    version = GetATLASVersion(env, 'lapack')
    env.Append(CPPDEFINES = [('ATLAS_INFO', '"\\"%s"\\"' % version)])
else:
    env.Append(CPPDEFINES = [('NO_ATLAS_INFO', 1)])

if config.CheckCLAPACK():
    has_clapack = 1
else:
    has_clapack = 0

config.Finish()
write_info(env)

#==========
#  Build
#==========
env.AppendUnique(CPPPATH = env['F2PYINCLUDEDIR'])
env.AppendUnique(F2PYOPTIONS = '--quiet')

env['BUILDERS']['GenerateFakePyf'] = Builder(action = do_generate_fake_interface,
                                  emitter = generate_interface_emitter)

#------------
#   flapack
#------------
yop = env.NumpyFromFTemplate('flapack.pyf', 'flapack.pyf.src')
env.NumpyPythonExtension('flapack', source = ['flapack.pyf'])

#------------
#   clapack
#------------
if has_clapack:
    env.NumpyFromFTemplate('clapack.pyf', 'clapack.pyf.src')
else:
    env.GenerateFakePyf('clapack', 'clapack.pyf.src')
env.NumpyPythonExtension('clapack', source = 'clapack.pyf')

#----------------
# calc_lwork:
#----------------
calc_src = env.F2py(pjoin(env['build_dir'], 'calc_lworkmodule.c'), 
                    source = pjoin(env['build_dir'], 'calc_lwork.f'))
env.NumpyPythonExtension('calc_lwork', source = calc_src + ['calc_lwork.f'])

#--------------
# Atlas version
#--------------
env.NumpyPythonExtension('atlas_version', 'atlas_version.c')
