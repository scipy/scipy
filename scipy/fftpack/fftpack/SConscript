# Last Change: Tue Oct 28 02:00 PM 2008 J
# vim:syntax=python
from os.path import join as pjoin

from numpy.distutils.misc_util import get_numpy_include_dirs
from numscons import GetNumpyEnvironment
from numscons import CheckF77Clib

env = GetNumpyEnvironment(ARGUMENTS)
env.Tool('f2py')

env.AppendUnique(CPPPATH = ['src'])
# env.AppendUnique(CPPPATH = get_numpy_include_dirs())
# env.AppendUnique(CPPPATH = env['F2PYINCLUDEDIR'])

config = env.NumpyConfigure(custom_tests = {'CheckF77Clib' : CheckF77Clib})
if not config.CheckF77Clib():
    raise Exception("Could not get f77/c++ link information")
config.Finish()

# Build dfftpack
src = env.Glob(pjoin('DFFTPACK', '*.f'))
dfftpack = env.DistutilsStaticExtLibrary('dfftpack', source = [str(s) for s in src])
env.PrependUnique(LIBS = ['dfftpack'])
env.PrependUnique(LIBPATH = '.')

# Build the fftpack wrapper
src = [pjoin("src/fftpack", i) for i in ['zfft.cxx','drfft.cxx', 'zfftnd.cxx']]
env.NumpyPythonExtension('_fftpack', src + ['src/zrfft.c', 'fftpack.pyf'])

# Build convolve
src = ['src/fftpack/convolve.cxx',  'convolve.pyf']
env.NumpyPythonExtension('convolve', src)
