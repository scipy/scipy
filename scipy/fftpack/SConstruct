# Last Change: Mon May 05 07:00 PM 2008 J
# vim:syntax=python
from os.path import join as pjoin

from numpy.distutils.misc_util import get_numpy_include_dirs
from numscons import GetNumpyEnvironment, write_info
from numscons import CheckFFT, IsMKL, IsFFTW2, IsFFTW3
from numscons import CheckF77Clib

env = GetNumpyEnvironment(ARGUMENTS)
env.Tool('numpyf2py')

env.AppendUnique(CPPPATH = get_numpy_include_dirs())
env.AppendUnique(CPPPATH = env['F2PYINCLUDEDIR'])
backends_paths = [pjoin(env['build_dir'], 'src', b)
                  for b in ["mkl", "fftw", "djbfft", "fftpack"]]
env.AppendUnique(CPPPATH = backends_paths)
env.AppendUnique(CPPPATH = [pjoin(env['build_dir'], "src")])

# Check fft implementation
config = env.NumpyConfigure(custom_tests = {'CheckFFT': CheckFFT,
                            'CheckF77Clib' : CheckF77Clib})
has_fft = config.CheckFFT()
if not config.CheckF77Clib():
    raise Exception("Could not get f77/c++ link information")
config.Finish()
write_info(env)

# Tweak defineds depending on the fft used
if has_fft:
    if IsMKL(env, 'fft'):
        env.Append(CPPDEFINES = "SCIPY_MKL_H")
    elif IsFFTW3(env, 'fft'):
        env.Append(CPPDEFINES = "SCIPY_FFTW3_H")
    elif IsFFTW2(env, 'fft'):
        env.Append(CPPDEFINES = "SCIPY_FFTW2_H")
    else:
        pass

# Build dfftpack
src = env.NumpyGlob(pjoin('dfftpack', '*.f'))
dfftpack = env.NumpyStaticExtLibrary('dfftpack', source = [str(s) for s in src])
env.PrependUnique(LIBS = ['dfftpack'])
env.PrependUnique(LIBPATH = env['build_dir'])

# Build _fftpack
src = ['src/zfft.cxx','src/drfft.c','src/zrfft.c', 'src/zfftnd.c', 'fftpack.pyf']
env.NumpyPythonExtension('_fftpack', src)

# Build convolve
src = ['src/convolve.c',  'convolve.pyf']
env.NumpyPythonExtension('convolve', src)
