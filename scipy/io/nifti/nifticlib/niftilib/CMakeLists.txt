#File Generated by Hans J. Johnson
#Please contact hans-johnson@uiowa.edu for making enhancments/corrections
PROJECT(NIFTILIB)

# use custom so revision
SET( NIFTILIB_SHAREDLIB_VERSION "1.0.0" )

# Apply the common nifti library version by default. To change this behaviour
# simply define NIFTILIB_SHAREDLIB_VERSION before this line
IF (NOT DEFINED NIFTILIB_SHAREDLIB_VERSION)
  SET (NIFTILIB_SHAREDLIB_VERSION ${NIFTI_SHAREDLIB_VERSION})
ENDIF (NOT DEFINED NIFTILIB_SHAREDLIB_VERSION)

# Extract soname from version string.
STRING( REGEX MATCH "^[0-9]+" NIFTILIB_SHAREDLIB_SOVERSION ${NIFTILIB_SHAREDLIB_VERSION})

INCLUDE_DIRECTORIES(${ZNZLIB_SOURCE_DIR})
SET(NIFTILIB_SRC nifti1_io.c)

SET(NIFTI_NIFTILIB_NAME ${PACKAGE_PREFIX}niftiio)
ADD_LIBRARY(${NIFTI_NIFTILIB_NAME} ${NIFTILIB_SRC} )
TARGET_LINK_LIBRARIES( ${NIFTI_NIFTILIB_NAME} ${PACKAGE_PREFIX}znz)

# Set library version when building shared libs.
IF (BUILD_SHARED_LIBS)
  SET_TARGET_PROPERTIES(${NIFTI_NIFTILIB_NAME} PROPERTIES SOVERSION ${NIFTILIB_SHAREDLIB_SOVERSION} VERSION ${NIFTILIB_SHAREDLIB_VERSION})
ENDIF (BUILD_SHARED_LIBS)

IF(UNIX)
  TARGET_LINK_LIBRARIES(${NIFTI_NIFTILIB_NAME} -lm)
ENDIF(UNIX)

IF(ITK_LIBRARY_PROPERTIES)
  SET_TARGET_PROPERTIES(${NIFTI_NIFTILIB_NAME} PROPERTIES ${ITK_LIBRARY_PROPERTIES})
ENDIF(ITK_LIBRARY_PROPERTIES)

IF(NOT NIFTI_INSTALL_NO_LIBRARIES)
  INSTALL(TARGETS ${NIFTI_NIFTILIB_NAME}
    RUNTIME DESTINATION ${NIFTI_INSTALL_BIN_DIR} COMPONENT RuntimeLibraries
    LIBRARY DESTINATION ${NIFTI_INSTALL_LIB_DIR} COMPONENT RuntimeLibraries
    ARCHIVE DESTINATION ${NIFTI_INSTALL_LIB_DIR} COMPONENT Development)
ENDIF(NOT NIFTI_INSTALL_NO_LIBRARIES)

IF(NOT NIFTI_INSTALL_NO_DEVELOPMENT)
  FILE(GLOB __files "${CMAKE_CURRENT_SOURCE_DIR}/*.h")
  INSTALL(FILES ${__files}
    DESTINATION ${NIFTI_INSTALL_INCLUDE_DIR}
    COMPONENT Development)
ENDIF(NOT NIFTI_INSTALL_NO_DEVELOPMENT)
