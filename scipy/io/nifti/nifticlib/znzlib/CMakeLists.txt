#File Generated by Hans J. Johnson
#Please contact hans-johnson@uiowa.edu for making enhancments/corrections
PROJECT(ZNZLIB)
SET(ZNZLIB_SRC znzlib.c)
INCLUDE_REGULAR_EXPRESSION("^.*$")

# Apply the common nifti library version by default. To change this behaviour
# simply define ZNZLIB_SHAREDLIB_VERSION before this line
IF (NOT DEFINED ZNZLIB_SHAREDLIB_VERSION)
  SET (ZNZLIB_SHAREDLIB_VERSION ${NIFTI_SHAREDLIB_VERSION})
ENDIF (NOT DEFINED ZNZLIB_SHAREDLIB_VERSION)

# Extract soname from version string
STRING( REGEX MATCH "^[0-9]+" ZNZLIB_SHAREDLIB_SOVERSION ${ZNZLIB_SHAREDLIB_VERSION})

SET(NIFTI_ZNZLIB_NAME ${PACKAGE_PREFIX}znz)

ADD_LIBRARY(${NIFTI_ZNZLIB_NAME} ${ZNZLIB_SRC} )
TARGET_LINK_LIBRARIES( ${NIFTI_ZNZLIB_NAME} ${NIFTI_ZLIB_LIBRARIES} )
IF(ITK_LIBRARY_PROPERTIES)
  SET_TARGET_PROPERTIES(${NIFTI_ZNZLIB_NAME} PROPERTIES ${ITK_LIBRARY_PROPERTIES})
ENDIF(ITK_LIBRARY_PROPERTIES)

# Set library version if building shared libs.
IF (BUILD_SHARED_LIBS)
  SET_TARGET_PROPERTIES(${NIFTI_ZNZLIB_NAME} PROPERTIES SOVERSION ${ZNZLIB_SHAREDLIB_SOVERSION} VERSION ${ZNZLIB_SHAREDLIB_VERSION})
ENDIF (BUILD_SHARED_LIBS)


IF(NOT NIFTI_INSTALL_NO_LIBRARIES)
  INSTALL(TARGETS ${NIFTI_ZNZLIB_NAME}
    RUNTIME DESTINATION ${NIFTI_INSTALL_BIN_DIR} COMPONENT RuntimeLibraries
    LIBRARY DESTINATION ${NIFTI_INSTALL_LIB_DIR} COMPONENT RuntimeLibraries
    ARCHIVE DESTINATION ${NIFTI_INSTALL_LIB_DIR} COMPONENT Development)
ENDIF(NOT NIFTI_INSTALL_NO_LIBRARIES)

IF(NOT NIFTI_INSTALL_NO_DEVELOPMENT)
  FILE(GLOB __files "${CMAKE_CURRENT_SOURCE_DIR}/*.h")
  INSTALL(FILES ${__files}
    DESTINATION ${NIFTI_INSTALL_INCLUDE_DIR}
    COMPONENT Development)
ENDIF(NOT NIFTI_INSTALL_NO_DEVELOPMENT)

