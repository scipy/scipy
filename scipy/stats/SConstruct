# Last Change: Wed Mar 05 09:00 PM 2008 J
# vim:syntax=python
from os.path import join as pjoin

from numpy.distutils.misc_util import get_numpy_include_dirs
from numscons import GetNumpyEnvironment, CheckF77Clib

env = GetNumpyEnvironment(ARGUMENTS)

env.AppendUnique(CPPPATH = [get_numpy_include_dirs()])
env.AppendUnique(CPPPATH = env['F2PYINCLUDEDIR'])

config = env.NumpyConfigure(custom_tests = {'CheckF77Clib' : CheckF77Clib})
if not config.CheckF77Clib():
    raise RuntimeError("Could not get C/F77 runtime information")
config.Finish()

# Statlib library
src = env.NumpyGlob(pjoin('statlib', '*.f' ))
env.NumpyStaticExtLibrary('statlibimp', source = src)

env.AppendUnique(LIBPATH = [env['build_dir']])

# Statlib extension
env.NumpyPythonExtension('statlib', source = 'statlib.pyf', 
                         F2PYOPTIONS = ["--no-wrap-functions"],
                         LIBS = 'statlibimp',
                         LINKFLAGSEND = env['F77_LDFLAGS'])

# futil extension
futil_src = env.F2py(pjoin(env['build_dir'], 'futilmodule.c'), 
                     pjoin(env['build_dir'], 'futil.f'))
env.NumpyPythonExtension('futil', source = futil_src + ['futil.f'],
                         LINKFLAGSEND = env['F77_LDFLAGS'])

# mvn extension
env.NumpyPythonExtension('mvn', source = ['mvn.pyf', 'mvndst.f'],
                         LINKFLAGSEND = env['F77_LDFLAGS'])
