# Last Change: Sat May 03 02:00 PM 2008 J
# vim:syntax=python
from os.path import join as pjoin

from numpy.distutils.misc_util import get_numpy_include_dirs
from numscons import GetNumpyEnvironment, CheckF77Clib

env = GetNumpyEnvironment(ARGUMENTS)

t = env.Tool('numpyf2py')

env.AppendUnique(CPPPATH = [get_numpy_include_dirs()])
env.AppendUnique(CPPPATH = env['F2PYINCLUDEDIR'])

config = env.NumpyConfigure(custom_tests = {'CheckF77Clib' : CheckF77Clib})
if not config.CheckF77Clib():
    raise RuntimeError("Could not get C/F77 runtime information")
config.Finish()

# Statlib library
src = env.NumpyGlob(pjoin('statlib', '*.f' ))
env.NumpyStaticExtLibrary('statlibimp', source = src)

env.AppendUnique(LIBPATH = [env['build_dir']])

# Statlib extension
env.DistutilsPythonExtension('statlib', source = 'statlib.pyf', 
                         F2PYOPTIONS = ["--no-wrap-functions"],
                         LIBS = 'statlibimp',
                         LINKFLAGSEND = env['F77_LDFLAGS'])

# futil extension
futil_src = env.NumpyF2py(pjoin('futilmodule.c'), pjoin('futil.f'))
env.DistutilsPythonExtension('futil', source = futil_src + ['futil.f'],
                         LINKFLAGSEND = env['F77_LDFLAGS'])

# mvn extension
env.DistutilsPythonExtension('mvn', source = ['mvn.pyf', 'mvndst.f'],
                         LINKFLAGSEND = env['F77_LDFLAGS'])
